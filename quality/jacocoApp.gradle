apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'

jacoco {
    toolVersion = '0.8.1'
}

tasks.withType(Test) {
    // Required for correct coverage of Robolectric tests
    jacoco.includeNoLocationClasses = true
}

task testCoverage(type: JacocoReport, dependsOn: "testEnvProdMinApi21DebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports on the Debug build."

    def excludedDirs = [
            '**/R.class',
            '**/R$*.class',
            '**/BuildConfig.*',
            '**/*$Lambda$*.*',
            '**/*Module.*',
            '**/*Dagger*.*',
            '**/*MembersInjector*.*',
            '**/*_Provide*Factory*.*',
            '**/Manifest*.*',
            '**/*Activity*.*',
            '**/*Dialog.*',
            '**/*Fragment*.*',
            '**/*ViewHolder*.*',
            '**/*Adapter*.*',
            '**/*zxing**',
            '**/*customviews**',
            '**/*databinding**',
            '**/injection**',
            '**/directory**',
            '**/adapter**',
            '**/models**',
            '**/extensions**',
            '**/*$ViewInjector*.*',
            '**/*$ViewBinder*.*',
            '**/*_Factory.*',
            '**/*$*$*.*' // Anonymous classes generated by kotlin
    ]
    def javaDebugTree = fileTree(
            dir: "${project.buildDir}/intermediates/classes/envProdMinApi21/debug",
            excludes: excludedDirs)
    def kotlinDebugTree = fileTree(
            dir: "${project.buildDir}/tmp/kotlin-classes/envProdMinApi21Debug",
            excludes: excludedDirs)

    def coverageSourceDirs = [
            "src/main/java"
    ]

    classDirectories = files([javaDebugTree], [kotlinDebugTree])
    additionalSourceDirs = files(coverageSourceDirs)
    sourceDirectories = files(coverageSourceDirs)
    executionData = files("${project.buildDir}/jacoco/testEnvProdMinApi21DebugUnitTest.exec")

    reports {
        xml.enabled = true
        html.enabled = true
    }
}