apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-allopen'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.novoda.build-properties'
apply from: '../quality/jacocoApp.gradle'
apply from: '../quality/ktlint.gradle'

buildProperties {
    secrets {
        using project.file('secrets.properties')
    }
}

android {
    compileSdkVersion Versions.compileSdk
    buildToolsVersion Versions.buildTools
    dexOptions.javaMaxHeapSize "3g"

    defaultConfig {
        applicationId "piuk.blockchain.android"
        targetSdkVersion Versions.targetSdk
        versionCode Versions.versionCode
        versionName Versions.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        archivesBaseName = "blockchain-$versionName"
        // Flag to hide or show the Contacts feature
        buildConfigBoolean "CONTACTS_ENABLED", false
        buildConfigString "SHAPE_SHIFT_API_KEY", buildProperties.secrets['shapeShiftApiKey']
        manifestPlaceholders = [FABRIC_API_KEY: buildProperties.secrets['fabricApiKey'].string]
    }
    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            multiDexEnabled false
            buildConfigBoolean "DOGFOOD", false
            buildConfigBoolean "USE_CRASHLYTICS", true
        }
        debug {
            multiDexEnabled true
            buildConfigBoolean "DOGFOOD", false
            buildConfigBoolean "USE_CRASHLYTICS", false
        }
        dogfood {
            initWith release
            debuggable true
            buildConfigBoolean "DOGFOOD", true
            applicationIdSuffix ".dogfood"
            versionNameSuffix '-DOGFOOD'
            signingConfig signingConfigs.debug
        }
    }

    flavorDimensions "environment", "minSdk"

    productFlavors {
        /**
         * API LEVELS
         */
        minApi16 {
            dimension "minSdk"
            minSdkVersion Versions.minSdk
        }
        minApi21 {
            // Specifically to avoid Multidexing on CI, which is causing OOM errors for now
            // This should be removed when possible
            dimension "minSdk"
            minSdkVersion 21
        }
        /**
         * Environments
         */
        envProd {
            dimension "environment"
            buildConfigString "EXPLORER_URL", buildProperties.secrets['prodExplorer']
            buildConfigString "API_URL", buildProperties.secrets['prodApiServer']
            buildConfigString "BITCOIN_WEBSOCKET_URL", buildProperties.secrets['prodBtcWebsocket']
            buildConfigString "ETHEREUM_WEBSOCKET_URL", buildProperties.secrets['prodEthWebsocket']
            buildConfigString "BITCOIN_CASH_WEBSOCKET_URL", buildProperties.secrets['prodBchWebsocket']
            buildConfigField "String", "ENVIRONMENT", '"env_prod"'
        }
        envDev {
            dimension "environment"
            buildConfigString "EXPLORER_URL", buildProperties.secrets['devExplorer']
            buildConfigString "API_URL", buildProperties.secrets['devApiServer']
            buildConfigString "BITCOIN_WEBSOCKET_URL", buildProperties.secrets['devBtcWebsocket']
            buildConfigString "ETHEREUM_WEBSOCKET_URL", buildProperties.secrets['devEthWebsocket']
            buildConfigString "BITCOIN_CASH_WEBSOCKET_URL", buildProperties.secrets['devBchWebsocket']
            buildConfigField "String", "ENVIRONMENT", '"env_dev"'
            versionNameSuffix '-dev'
        }
        envStaging {
            dimension "environment"
            buildConfigString "EXPLORER_URL", buildProperties.secrets['stagingExplorer']
            buildConfigString "API_URL", buildProperties.secrets['stagingApiServer']
            buildConfigString "BITCOIN_WEBSOCKET_URL", buildProperties.secrets['stagingBtcWebsocket']
            buildConfigString "ETHEREUM_WEBSOCKET_URL", buildProperties.secrets['stagingEthWebsocket']
            buildConfigString "BITCOIN_CASH_WEBSOCKET_URL", buildProperties.secrets['stagingBchWebsocket']
            buildConfigField "String", "ENVIRONMENT", '"env_staging"'
            versionNameSuffix '-staging'
        }
        envTestnet {
            dimension "environment"
            buildConfigString "EXPLORER_URL", buildProperties.secrets['testnetExplorer']
            buildConfigString "API_URL", buildProperties.secrets['testnetApiServer']
            buildConfigString "BITCOIN_WEBSOCKET_URL", buildProperties.secrets['testnetBtcWebsocket']
            buildConfigString "ETHEREUM_WEBSOCKET_URL", buildProperties.secrets['testnetEthWebsocket']
            buildConfigString "BITCOIN_CASH_WEBSOCKET_URL", buildProperties.secrets['testnetBchWebsocket']
            buildConfigField "String", "ENVIRONMENT", '"env_testnet"'
            versionNameSuffix '-testnet'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        disable 'MissingTranslation', 'ObsoleteLintCustomCheck'
        checkReleaseBuilds true
        fatal 'StopShip'
        htmlReport true
        htmlOutput file("build/reports/lint-report.html")
        abortOnError false
        baseline file("lint-baseline.xml")
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/rxjava.properties'
    }
    dataBinding.enabled true

    testOptions {
        // Fix for Robolectric bug when using AAPT2
        unitTests.includeAndroidResources true
        // Always show the result of every unit test, even if it passes.
        unitTests.all {
            testLogging {
                events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
            }
            reports.junitXml.destination = file('../build/test-results/')
        }
    }
}

kapt {
    useBuildCache = true
}

allOpen {
    annotation("piuk.blockchain.androidcore.utils.annotations.Mockable")
}

androidExtensions {
    experimental = true
}

dependencies {
    implementation project(':coreui')
    implementation project(':buysellui')

    implementation Libraries.multidex
    kapt Libraries.dataBindingKapt
    // Google services
    implementation Libraries.firebaseMessaging
    implementation Libraries.googlePlayServicesBase
    // Utils
    implementation Libraries.yearclass
    // Dagger
    kapt Libraries.daggerKapt
    // Views
    implementation(Libraries.zxing) { transitive = true }
    implementation Libraries.countryPicker
    implementation Libraries.bottomNav
    implementation Libraries.circleIndicator
    implementation Libraries.charts

    // UI/Instrumentation Tests
    androidTestImplementation(Libraries.espresso) {
        exclude group: 'javax.inject', module: 'support-annotations'
    }
    // Unit Tests
    testImplementation Libraries.junit
    testImplementation Libraries.mockito
    testImplementation Libraries.hamcrestJunit
    testImplementation Libraries.kotlinJunit
    testImplementation Libraries.kluent
    testImplementation(Libraries.mockitoKotlin) {
        ['kotlin-stdlib', 'kotlin-reflect'].each { exclude module: it }
    }
    testImplementation(Libraries.robolectric) {
        exclude group: 'com.google.guava'
        exclude group: 'com.google.protobuf'
    }
    testImplementation Libraries.json
}

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath Libraries.fabricTools
        classpath Libraries.buildProperties
    }
}

repositories {
    jcenter()
    // Required for CountryPicker
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
}

// This must remain at the bottom of this file until Google work out a better way to do this
apply plugin: 'com.google.gms.google-services'